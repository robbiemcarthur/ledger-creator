plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.0.26'
    id 'org.owasp.dependencycheck' version '11.1.1'
}

group = 'org.creatorledger'
version = generateVersion()
description = 'creator-ledger'

def generateVersion() {
    def baseVersion = '0.1.0'
    def timestamp = new java.text.SimpleDateFormat('yyyyMMdd.HHmmss').format(new Date())
    return "${baseVersion}-${timestamp}"
}

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

repositories {
    mavenCentral()
}

ext {
    groovyVersion = '4.0.23'
    spockVersion = '2.4-M4-groovy-4.0'
    testcontainersVersion = '1.20.2'
    springModulithVersion = '1.2.4'
}

dependencies {
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    testImplementation "org.apache.groovy:groovy:${groovyVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework:spring-aop'
    implementation 'org.aspectj:aspectjweaver'
    implementation "org.springframework.modulith:spring-modulith-starter-core:${springModulithVersion}"
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation "org.testcontainers:spock:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.springframework.modulith:spring-modulith-starter-test:${springModulithVersion}"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
    }
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/CreatorLedgerApplication.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/CreatorLedgerApplication.class'
            ])
        }))
    }
}

spotbugs {
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'medium'
    excludeFilter = file('config/spotbugs/exclude.xml')
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

// Disable SpotBugs on test code (Groovy/Spock generates closures that trigger false positives)
tasks.named('spotbugsTest') {
    enabled = false
}

dependencyCheck {
    format = 'ALL'
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/dependency-check/suppressions.xml'
    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

check.dependsOn jacocoTestReport, jacocoTestCoverageVerification, spotbugsMain